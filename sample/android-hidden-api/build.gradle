apply plugin: 'com.android.library'

group = 'com.anggrayudi'
version = '0.0.6'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    compile 'com.android.support:support-annotations:24.2.0'
    compile 'com.android.support:support-v4:24.2.0'
}

def modifiedJar = project.rootProject.file('../'+ android.compileSdkVersion +'/android.jar')
def originalJar = file("${android.getSdkDirectory().getAbsolutePath()}/platforms/"+ android.compileSdkVersion +'/original/android.jar')

task copyModifiedAndroidJar(type: Copy){
    from modifiedJar
    into originalJar.parentFile.parent
}

task copyOriginalAndroidJar(type: Copy){
    from originalJar
    into originalJar.parentFile.parent
}

ext {
    PUBLISH_GROUP_ID = group
    PUBLISH_ARTIFACT_ID = 'android-hidden-api'
    PUBLISH_VERSION = version
    siteUrl = 'https://github.com/anggrayudi/android-hidden-api'
    gitUrl = 'https://github.com/anggrayudi/android-hidden-api.git'
    libsDesc = 'An utility library that provides access to Android internal resources (com.android.internal.R).'
}

apply from: '../android-release-aar.gradle'